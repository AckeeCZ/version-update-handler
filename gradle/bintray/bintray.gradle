apply plugin: 'com.jfrog.bintray'

version = project.property("VERSION_NAME")

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}


// Bintray
Properties properties = new Properties()
def bintrayUser
def bintrayApiKey

// Use local.properties if uploading manually or github secrets if uploading via CI
if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    bintrayUser = properties.getProperty("bintray.user")
    bintrayApiKey = properties.getProperty("bintray.apikey")
} else {
    bintrayUser = System.getenv("bintrayUsername")
    bintrayApiKey = System.getenv("bintrayApiKey")
}

bintray {
    user = bintrayUser
    key = bintrayApiKey

    configurations = ['archives']
    pkg {
        name = bintrayName
        repo = project.property("BINTRAY_REPO")
        userOrg = project.property("BINTRAY_ORGANISATION")
        desc = libraryDescription
        websiteUrl = project.property("SITE_URL")
        vcsUrl = project.property("GIT_URL")
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            name = project.property("VERSION_NAME")
            released = new Date()

            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
//                passphrase = properties.getProperty("bintray.gpg.password")  //Optional. The passphrase for GPG signing'
            }
        }
    }
}